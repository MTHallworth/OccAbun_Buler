
model {
###################################################################################
#
#             Priors
#
################################################################################### 
# Hyperpriors
mu.lpsi ~ dnorm(0,0.01)
tau.lpsi <- pow(sd.lpsi, -2)
sd.lpsi ~ dunif(0,8)

mu.beta ~ dnorm(0,0.01)
tau.beta <- pow(sd.beta, -2)
sd.beta ~ dunif(0,5)

mu.lp ~ dnorm(0,0.01)
tau.lp <- pow(sd.lp, -2)
sd.lp ~ dunif(0,3)

mu.betaP ~ dnorm(0,0.01)
tau.betaP <- pow(sd.betaP, -2)
sd.betaP ~ dunif(0,3)

# priors 
   lpsi ~ dnorm(mu.lpsi, tau.lpsi)               # Hyperparams
   lp ~ dnorm(mu.lp, tau.lp)

for(c in 1:ncovs){                                  # Richness Covs
   beta[c] ~ dnorm(mu.beta, tau.beta)
 } #ncovs
 
for(d in 1:pcovs){                                  # Detection Covs
   betaP[d] ~ dnorm(mu.betaP, tau.betaP)
}  #pcovs


##################################################################################
#
# Likelihood
# Ecological model for true occurrence (process model)
#
##################################################################################

for (i in 1:plots) { 

      logit(psi[i])<-lpsi + beta[1]*Elev[i]+beta[2]*Elev2[i]+beta[3]*Slope[i] + beta[4]*Aspect[i]

      z[i] ~ dbern(psi[i])

} #nplots

##################################################################################
#
# Observation model for replicated detection/nondetection observations
#
##################################################################################

   for (i in 1:plots){
      for(j in 1:reps){

         logit(p[i,j]) <-lp+betaP[1]*time[i,j]+betaP[2]*date[i,j]+betaP[3]*obsvr[i,j] 

         y[i,j] ~ dbern(z[i] * p[i,j]) # True Occupancy state * detection 

      } #nreps
   }    #nschwarz
   
##################################################################################
#
# Derived quantities
#
##################################################################################


##################################################################################

} # End Model

